start ::= "USER " username "\nPASS " password "\n" cmds
;

cmds ::=
    "QUIT\n"
//  | "REIN\n" start
  | cmd "\n" cmds
;

cmd ::=
    no_arg_cmd
  | upload_cmd " " filename
  | "REST " int "\n" appe_or_stor " " filename
  | "REST " int "\nRETR " filename
  | "RNFR " filename "\nRNTO " filename
  | dir_cmd " " dirname
  | file_cmd " " filename
  | "MODE " s_b_c
  | "STRU " f_r_p
  | "TYPE " type_param
  | "PORT " int "," int "," int "," int "," int "," int
;

no_arg_cmd ::= "NOOP" | "CDUP" | "PWD" | "HELP" | "SYST" | "STAT" | "SITE" | "ABOR" | "LIST" | "NLST" | "PASV";
upload_cmd ::= "APPE" | "STOR" | "STOU";
appe_or_stor ::= "APPE" | "STOR";
dir_cmd ::= "CWD" | "MKD" | "RMD" | "LIST" | "NLST" | "STAT" | "SMNT";
file_cmd ::= "DELE" | "RETR" | "LIST" | "NLST" | "STAT";
s_b_c ::= "S" | "B" | "C";
f_r_p ::= "F" | "R" | "P";
type_param ::= "A N" | "A T" | "A C" | "E N" | "E T" | "E C" | "I" | "L " digit;

dirname ::= "/" relative_path | relative_path;
relative_path ::= filename | filename "/" relative_path;

// digit, letter, and stringN are built-in terminals defined in the generated C code.

filename ::= letter | letter filename;
username ::= string3;
password ::= string2;
int ::= digit | digit int;
